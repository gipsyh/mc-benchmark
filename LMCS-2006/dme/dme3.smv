MODULE and-gate(in1,in2)
VAR
  out : boolean;
ASSIGN
  init(out) := FALSE;
  next(out) := (in1 & in2) union out;

MODULE and_gate_init(in1,in2,init_out)
VAR
  out : boolean;
ASSIGN
  init(out) := init_out;
  next(out) := (in1 & in2) union out;

MODULE or-gate(in1,in2)
VAR
  out : boolean;
ASSIGN
  init(out) := FALSE;
  next(out) := (in1 | in2) union out;

MODULE c-element(in1,in2)
VAR
  out : boolean;
ASSIGN
  init(out) := FALSE;
  next(out) := 
    case
      in1 = in2 : in1 union out;
      TRUE : out;
    esac;

MODULE mutex-half(inp,other-out)
VAR
  out : boolean;
ASSIGN
  init(out) := FALSE;
  next(out) := inp union out;
TRANS
  !(next(out) & next(other-out))

MODULE user
VAR
  req : boolean;
ASSIGN
  init(req) := FALSE;
  next(req) := (!ack) union req;

MODULE cell(left,right,token)
VAR
  u : user;
  a : mutex-half(u.req,b.out);
  b : mutex-half(left.req,a.out);
  c : and-gate(a.out,!left.ack);
  d : and-gate(b.out,!u.ack);
  g : or-gate(c.out,d.out);
  e : c-element(c.out,i.out);
  f : c-element(d.out,i.out);
  h : c-element(g.out,j.out);
  k : and-gate(g.out,!h.out);
  i : and-gate(h.out,!j.out);
  l : and-gate(k.out,m.out);
  j : or-gate(l.out,ack);
  p : and-gate(k.out,n.out);
  n : and_gate_init(!e.out,!m.out,!token);
  m : and_gate_init(!f.out,!n.out,token);
  r : and-gate(e.out,m.out);
  q : and-gate(f.out,n.out);
DEFINE
  req := p.out;
  left.ack := q.out;
  u.ack := r.out;

MODULE main
VAR
  e_3 : process cell(e_1,e_2,TRUE);
  e_2 : process cell(e_3,e_1,FALSE);
  e_1 : process cell(e_2,e_3,FALSE);

-- p1
LTLSPEC
G(((e_3.ack)&(X(!(e_3.ack))))->(X((G(!(e_3.ack)))|(((!(e_3.ack))U(e_2.ack))U(e_1.ack)))))
-- !p1
LTLSPEC
!(G(((e_3.ack)&(X(!(e_3.ack))))->(X((G(!(e_3.ack)))|(((!(e_3.ack))U(e_2.ack))U(e_1.ack))))))
-- ptimo
LTLSPEC
G((e_2.u.ack)->((e_2.u.ack)T((!(e_2.u.ack))T(!(e_1.req)))))

-- !ptimo
LTLSPEC
!(G((e_2.u.ack)->((e_2.u.ack)T((!(e_2.u.ack))T(!(e_1.req))))))

-- ptimonegnv
LTLSPEC
!((G((e_2.u.ack)->((e_2.u.ack)T((!(e_2.u.ack))T(!(e_1.req))))))&(G(F(e_2.u.ack))))
