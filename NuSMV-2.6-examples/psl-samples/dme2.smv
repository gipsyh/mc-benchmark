MODULE and-gate(in1,in2)
VAR
  out : boolean;
ASSIGN
  init(out) := FALSE;
  next(out) := (in1 & in2) union out;

MODULE and-gate-init(in1,in2,init-out)
VAR
  out : boolean;
ASSIGN
  init(out) := init-out;
  next(out) := (in1 & in2) union out;

MODULE or-gate(in1,in2)
VAR
  out : boolean;
ASSIGN
  init(out) := FALSE;
  next(out) := (in1 | in2) union out;

MODULE c-element(in1,in2)
VAR
  out : boolean;
ASSIGN
  init(out) := FALSE;
  next(out) := 
    case
      in1 = in2 : in1 union out;
      TRUE : out;
    esac;

MODULE mutex-half(inp,other-out)
VAR
  out : boolean;
ASSIGN
  init(out) := FALSE;
  next(out) := inp union out;
TRANS
  !(next(out) & next(other-out))

MODULE user
VAR
  req : boolean;
ASSIGN
  init(req) := FALSE;
  next(req) := (!ack) union req;

MODULE cell(left,right,token)
VAR
  u : user;
  a : mutex-half(u.req,b.out);
  b : mutex-half(left.req,a.out);
  c : and-gate(a.out,!left.ack);
  d : and-gate(b.out,!u.ack);
  g : or-gate(c.out,d.out);
  e : c-element(c.out,i.out);
  f : c-element(d.out,i.out);
  h : c-element(g.out,j.out);
  k : and-gate(g.out,!h.out);
  i : and-gate(h.out,!j.out);
  l : and-gate(k.out,m.out);
  j : or-gate(l.out,ack);
  p : and-gate(k.out,n.out);
  n : and-gate-init(!e.out,!m.out,!token);
  m : and-gate-init(!f.out,!n.out,token);
  r : and-gate(e.out,m.out);
  q : and-gate(f.out,n.out);
DEFINE
  req := p.out;
  left.ack := q.out;
  u.ack := r.out;

MODULE main
VAR
  e_3 : process cell(e_1,e_2,TRUE);
  e_2 : process cell(e_3,e_1,FALSE);
  e_1 : process cell(e_2,e_3,FALSE);

SPEC
  AG (
        !(e_1.u.ack & e_2.u.ack)
      & !(e_1.u.ack & e_3.u.ack)
      & !(e_2.u.ack & e_3.u.ack)
     )

PSLSPEC
  AG (
        !(e_1.u.ack & e_2.u.ack)
      & !(e_1.u.ack & e_3.u.ack)
      & !(e_2.u.ack & e_3.u.ack)
     ) ;

PSLSPEC
  always (
        !(e_1.u.ack & e_2.u.ack)
      & !(e_1.u.ack & e_3.u.ack)
      & !(e_2.u.ack & e_3.u.ack)
     ) ;

PSLSPEC
  always eventually! forall i in {1,2} : e_1.u.ack;

PSLSPEC
  always (
--        !(forall i in {1,2} : e[i].u.ack)
        !(e_1.u.ack & e_2.u.ack)
--      & !(forall i in {1,3} : e[i].u.ack)
      & !(e_1.u.ack & e_3.u.ack)
--      & !(forall i in {2,3} : e[i].u.ack)
      & !(e_2.u.ack & e_3.u.ack)
     ) ;

PSLSPEC 
--        (never (forall i in {1,2} : e[i].u.ack))
        (never (e_1.u.ack & e_2.u.ack))
--      & (never (forall i in {1,3} : e[i].u.ack))
      & (never (e_1.u.ack & e_3.u.ack))
--      & (never (forall i in {2,3} : e[i].u.ack))
      & (never (e_2.u.ack & e_3.u.ack))
      ;



